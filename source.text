"islands & witches" by Betsy Haibel and Elizabeth Yalkut

Book 1 - the Map

Chapter 1 - Naming Islands & Other Lands

An island is a kind of room.

Troy is an island. "The wreck of a once-mighty city. A hill by a river mouth. Double defensive walls ring the city, covered in scorch marks and badly damaged. There are bodies piled against the walls. Bronze arrowheads and broken spear-shafts litter the ground. Fires smoulder in the streets."

Djerba is an island. "Lizards dangle from boughs of trees. A woman is wearing a white khiton. She is wearing gold and bronze pins in her hair."

Aeaea is an island. "Rosemary bushes as tall as a man. The air is full of scarlet and onyx bees, twice as big as they should be. A dozen ivory dice are lying in a bed of dark-green moss. Blindfolded herms flank a path from the shore into a grove of pine trees. There is a small stone house with yellow smoke emerging. Wolves, lions, and deer wander the island."

Thrinacia is an island. "Cows wander the island. A man is drinking pale yellow juice from a golden cup."

Ogygia is an island. "Apple trees are growing with white blossoms and blood-red fruit at the same time. Cypress trees reach to the sky. Birds, eerily silent, are flapping their wings; there are no bird’s-nests, but fallen feathers carpet the ground. Calypso’s hair is made of feathers."

Scheria is an island. "A rhyton in the shape of a dog’s head holds barley beer. Large white fabric sheets flutter in the breeze. Fawn-colored sighthounds lie in the shade."

Ithaka is an island. "The hills are made of pale cream and purple-gray stone. A woman is carrying a bronze-and-gold bowl with lead loom-weights in it. A woman is carrying a basket full of unwashed wool. A goat with yellow eyes follows you."

The Open Ocean is a room. "The water is dark. A raft is floating on the ocean. A bird is sitting on the raft. Seaweed is drying on the raft."

The Underworld is a room. "The air smells of lilies and blood. It is always just past sunset."

Chapter 2 - Layout

An island can be free-floating or stabilized. An island is usually free-floating.

Before going a direction (called way):
	let Elsewhere be the list of free-floating islands;
	if the location is an island:
		remove the location from Elsewhere;
	if the location is not Ithaka:
		remove Ithaka from Elsewhere;
	add the Underworld to Elsewhere;
	add Elsewhere to Elsewhere;
	add Elsewhere to Elsewhere;
	add Ithaka to Elsewhere;
	sort Elsewhere in random order;
	let next be entry 1 of Elsewhere;
	change the way exit of the location to next;
	let reverse be the opposite of way;
	change the reverse exit of next to the location.

To stabilize (i - an island):
	say "The ground rumbles and the breeze stills.";
	now i is stabilized.

Book 2 - the Witches

A witch is a kind of woman. A witch has a table name called the dialogue table. A witch has some text called the solution text.

Annoyance is a kind of value. The annoyances are calm, annoyed, exasperated, vexed, irked, irate, and furious.

Needing relates one witch to one thing. The verb to need means the needing relation.
Living on relates one witch to one island. The verb to live on means the living on relation.
Knowing relates one witch to various topics. The verb to know means the knowing relation.

The waterskin is a thing. The waterskin is in Hammerspace. The waterskin can be full or empty. The waterskin is full. Sourcing relates a thing to one room. The verb to be brought from means the sourcing relation. The waterskin is brought from Troy. The printed name of the waterskin is "waterskin ([if full]full of water from [the island to which the waterskin relates by the sourcing relation][otherwise]empty[end if])".

A branch is a kind of thing. A branch can be lit or unlit. A branch is usually unlit.

Chapter 1 - Persephone

Persephone is a witch. Persephone has an annoyance. Persephone is calm.

Table of Persephone's lines
annoyance	line
calm	"'How lovely to see you.' Persephone announces, as if to her court. You feel welcomed, perhaps excessively."
annoyed	"'You again?' Persephone says. She rolls her eyes. You feel embarrassed."
exasperated	"'Don\'t you have anywhere else to be?' Persephone asks. She holds up a hand before you can answer. You close your mouth abruptly."
vexed	"'Please start being interesting.' Persphone says. It is not a suggestion. You look at the floor."
irked	"'What do you think would be a fitting punishment for an uninvited guest,'' Persephone asks. You swallow hard."
irate	"Persephone leans back in her bone throne. 'You are not welcome here,' she says. Your skin begins to soften and dissolve."
furious	"Persephone shrieks, wordless and inhuman, like a fox crying. You curl into a ball around your knees."

Chapter 2 - Andromache

[Problem: Andromache must burn Astyanax's body.
Solution: Bring Andromache a rosemary branch from Aeaea.]

Andromache is a witch. Andromache needs a rosemary branch. Andromache has the solution text "WRITEME".

Test Andromache with "gonear andromache / gonear kirke / take rosemary branch / gonear andromache / give andromache rosemary branch".

Chapter 3 - Nameless

[THERE IS LITERALLY NO SOURCE THAT NAMES THE LOTOS-EATERS AS INDIVIDUALS WHAT THE FUCK.]

[Problem: Djerba's fire has gone out.
Solution: Bring fire from Troy.]

Nameless is a witch. Nameless needs a piece of driftwood. Nameless has the solution text "WRITEME".

Test Nameless with "gonear nameless / gonear andromache / take driftwood / gonear nameless / give nameless driftwood / light driftwood / gonear andromache / light driftwood / gonear nameless / give nameless driftwood".

Chapter 4 - Kirke

[Problem: Kirke's moly is malfunctioning and she can't choose what animal she turns visitors into.
Solution: Bring Kirke fresh water from Scheria.]

Kirke is a witch. Kirke needs a full waterskin brought from Scheria. Kirke has the solution text "WRITEME". Kirke lives on Aeaea. The dialogue table of Kirke is the Table of Kirke's responses.

Table of Kirke's responses
topic	reply
"moly"	"WRITEME [remember waterskin]"

Test Kirke with "gonear kirke / ask kirke about moly / give kirke waterskin / gonear nausikaa / fill waterskin / follow nausikaa / ask nausikaa about water / follow nausikaa / fill waterskin / gonear kirke / give kirke waterskin"

To say remember waterskin:
	If the waterskin is in Hammerspace:
		say "You ask one of your men to fetch a waterskin from the boat. He sprints there and back.";
		move the waterskin to the player.

To allow Kirke to be picky:
	if the waterskin is empty:
		say "She needs water, not an empty waterskin.";
		stop the action;
	if the waterskin is not brought from Scheria:
		let s be the the island to which the waterskin relates by the sourcing relation;
		choose the row with source of s from the Table of Kirke refusing water;
		say "KIrke frowns. '[refusal entry]'[paragraph break]";
		stop the action;
	continue the action.

Table of Kirke refusing water
source	refusal
Troy	"Dead water full of dead things."
Djerba	"WRITEME"
Aeaea	"WRITEME"
Thrinacia	"WRITEME"
Ogygia	"WRITEME"
Ithaka	"WRITEME"
Open Ocean	"WRITEME"
Underworld	"WRITEME"

Chapter 5 - Lampetia

Lampetia is a witch. The dialogue table of Lampetia is the Table of Lampetia's responses.

Table of Lampetia's responses
topic	reply
"cows"	"My father's."
"bees"	"[one of]Few too many of them if you ask me.[or]My father doesn't care one whit for them, and neither do I.[at random]"
"night and day"	"Lampetia nods.[paragraph break][stabilize Thrinacia]"

To say Lampetia's riddle:
	If Thrinacia is free-floating:
		say "There are two sisters: one gives birth to the other who then gives birth to the first. Who are they?".

Report asking Lampetia about something:
	say Lampetia's riddle;
	stop the action.

Report telling Lampetia about something:
	say Lampetia's riddle;
	stop the action.

Report answering Lampetia that something:
	say Lampetia's riddle;
	stop the action.

To say stabilize Thrinacia:
	stabilize Thrinacia;
	stop the action.

Test Lampetia with "gonear Lampetia / ask Lampetia about cows / ask lampetia about bees / answer lampetia night and day".

Chapter 6 - Calypso

[Problem: Calypso's wine is sour.
Solution: Bring Calypso bees from Thrinacia.]

Calypso is a witch. Calypso needs the beehive. Calypso has a solution text "WRITEME". Calypso lives on Ogygia.

Test Calypso with "gonear calypso / gonear lampetia / follow bees / take beehive / gonear calypso / give calypso beehive".

Chapter 7 - Nausikaa

Nausikaa is a witch. The dialogue table of Nausikaa is the Table of Nausikaa's responses.

Instead of following Nausikaa:
	if Nausikaa knows "water":
		now Scheria is watery;
		say "She leads you to a clear spring.";
	otherwise:
		say "Nausikaa looks at you skeptically. You stop, to keep her trust.".

Table of Nausikaa's responses
topic	reply
"water"	"WRITEME[learn about water]"

To say learn about water:
	now Nausikaa knows "water";

Chapter 8 - Penelope

Penelope is a witch. The dialogue table of Penelope is the Table of Penelope's responses.

Table of Penelope's responses
topic	reply
"silence/nothing"	"[win game]"

Before speech in the presence of Penelope for the first time:
	say "You greet your wife. She grimaces. 'Do not speak and you say my name. If you need to speak, you will say my name.'";
	stop the action.	

To say Penelope's riddle:
	say "[one of]Penelope interrupts you. [or]Penelope cuts you off with a sharp shake of her head. [at random]'Do not speak and you say my name. If you need to speak, you will say my name.'".

Report answering Penelope that something:
	say Penelope's riddle;
	stop the action.

To say win game:
	end the story saying "Penelope has recognized you. Your journey home has ended." instead.

Test Penelope with "gonear penelope / answer penelope that lies / answer penelope silence".
 
Book 3 - the Islands, and other Lands

A room can be watery or arid. A room is usually watery.

Chapter 1 - the Underworld

Persephone is in the Underworld. "[if the Underworld is unvisited]Persephone's biding her time here until spring.[otherwise]Persephone is still here.[end if] [entrance line]"

To say entrance line:
	let annoyance level be the annoyance of Persephone;
	let new line be the line corresponding to an annoyance of annoyance level in the Table of Persephone's lines;
	say new line.
	
Before going to the Underworld:
	if the Underworld is visited, increase Persephone's annoyance.

To increase Persephone's annoyance:
	if the annoyance of Persephone is less than furious, now the annoyance of Persephone is the annoyance after the annoyance of Persephone.
			
Chapter 2 - Troy

Andromache is in Troy.

A piece of driftwood is a branch. A piece of driftwood is in Troy.

Instead of burning the piece of driftwood:
	if the location is Troy:
		now the driftwood is lit;
		say "You turn the driftwood into a makeshift torch.";
	otherwise:
		say "You can't see any fire to light that from.".

Chapter 3 - Djerba

Nameless is in Djerba.

Chapter 4 - Aeaea

Kirke is in Aeaea.

The rosemary branch is a thing. The rosemary branch is in Aeaea.

Chapter 5 - Thrinacia

Lampetia is in Thrinacia.

The swarm is a thing. Understand "bees" as the swarm. Understand "bee" as the swarm. The swarm is in Thrinacia. "You hear bees buzzing about." Instead of taking the swarm, say "A bee stings your leather glove and dies."

The beehive is a thing. The beehive is in Hammerspace.

Instead of following the swarm:
	if the beehive is in Hammerspace or the beehive is in Thrinacia:
		say "You follow the bees over the island, gingerly sidestepping cows and their dung. You find a beehive, high in a tree.";
		move the beehive to Thrinacia;
	otherwise:
		say "The bees mill about aimlessly."

Chapter 6 - Ogygia

Calypso is in Ogygia.

Chapter 7 - Scheria

Nausikaa is in Scheria. Scheria is arid.

Chapter 8 - Ithaka

Penelope is in Ithaka.

Book 4 - Interactions

Chapter 1 - Speech

Asking someone about something is speech. Telling someone about something is speech. Answering someone that something is speech. Asking someone for something is speech.

Carry out telling a witch about something:
	repeat through the dialogue table of the noun:
		if the topic understood includes topic entry:
			say "[reply entry][paragraph break]";
			rule succeeds;
	say "[the noun] looks at you blankly.";
	stop the action.

Carry out asking a witch about something:
	repeat through the dialogue table of the noun:
		if the topic understood includes topic entry:
			say "[reply entry][paragraph break]";
			rule succeeds;
	say "[the noun] looks at you blankly.";
	stop the action.

Carry out answering a witch that something:
	repeat through the dialogue table of the noun:
		if the topic understood includes topic entry:
			say "[reply entry][paragraph break]";
			rule succeeds;
	say "[the noun] looks at you blankly.";
	stop the action.

Chapter 2 - Barter Puzzles

The block giving rule is not listed in the check giving it to rules.

Check giving (this is the polite refusal of unwanted objects rule):
	unless the noun interests the second noun: 
		say "[The second noun] refuses [the noun]." instead.

Report giving:
	say "You give [the second noun] [the noun]. She smiles."

After giving:
	solve the problem of the second noun.

To decide whether (item - a thing) interests (character - a person):
	if the character is Kirke and the item is the waterskin and the waterskin is full and the waterskin is brought from Scheria, yes;
	if the character is Kirke and the item is the waterskin:
		allow Kirke to be picky;
		no;
	if the character is Nameless and the item is the piece of driftwood and the item is lit, yes;
	if the character is Nameless and the item is the piece of driftwood and the item is unlit, no;
	if the character needs the item, yes;
	no.

To solve the problem of (witch - a witch):
	Say the solution text of the witch;
	Stabilize the island to which the witch relates by the living on relation.
	
Chapter 3 - Actions

Emptying is an action applying to one carried thing. Understand "empty [something preferably held]" as emptying.

Check emptying:
	unless the noun is the waterskin, say "You cannot empty [the noun]" instead;
	unless the noun is full, say "It's already empty" instead.

Carry out emptying: now the noun is empty. Report emptying: say "You dump water onto the sand.".

Filling is an action applying to one carried thing. Understand "fill [something preferably held]" as filling.

Before filling: if the noun is the waterskin and the noun is full, try emptying the waterskin.

Check filling:	
	unless the noun is the waterskin, say "You cannot fill [the noun]" instead;
	unless the noun is empty, say "It's already full" instead;
	unless the location is watery, say "You can't see any water from which to fill the waterskin." instead.

Carry out filling:
	now the noun is full;
	now the noun is brought from location.

Report filling:
	if location is the Open Ocean:
		say "You fill the waterskin with brine.";
	otherwise:
		say "You fill the waterskin from the springs of [location].".

Following is an action applying to one visible thing. Understand "follow [something]" as following.

Understand "scrounge for [something]" as taking. Understand "scrounge [something]" as taking.
