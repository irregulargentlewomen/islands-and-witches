"islands & witches" by Betsy Haibel and Elizabeth Yalkut

The story genre is "fantasy". The story description is "The wreck of a once-mighty city, and a long journey home." The story creation year is 2017.

Release along with an interpreter.

Book 1 - the Map

Chapter 1 - Naming Islands & Other Lands

An island is a kind of room.

Troy is an island. "The wreck of a once-mighty city. The remains of the city are on a hill by the mouth of the river Scamander. Double defensive walls ring the city, covered in scorch marks and badly damaged. There are bodies piled against the walls. Bronze arrowheads and broken spear-shafts litter the ground. Fires smoulder in the streets."

Djerba is an island. "Lizards dangle from boughs of trees. There are people sprawled on the ground, as though they have been felled by a blow, but there are no traces of a battle, and no one is wounded. Something is missing here, perhaps someone, but no one is mourning. No one pays attention to you as you walk toward a shrine wth a cold brazier and a heap of clay offering-figures."

Aeaea is an island. "Rosemary bushes as tall as a man. The air is full of scarlet and onyx bees, twice as big as they should be. A dozen ivory dice are lying in a bed of dark-green moss. Blindfolded herms flank a path from the shore into a grove of pine trees. There is a small stone house with yellow smoke emerging. Wolves, lions, and deer wander the island, nudging each other companiably. When they notice you, they go still."

Thrinacia is an island. "Cows wander the island, their horns curving fantastically. A man is drinking pale yellow juice from a golden cup. He looks at you, turns his cup over, and pours his drink on the ground." 

Ogygia is an island. "Apple trees are growing with white blossoms and blood-red fruit at the same time. Cypress trees reach to the sky. Birds, eerily silent, are flapping their wings; there are no bird’s-nests, but fallen feathers carpet the ground."

Scheria is an island. "A small table is laden with rhytons full of barley beer. A barbarian's drink, but you would not reject it after your journey. White fabric is draped over bushes. Nearby, boys wrestle and throw javelins at a practice target, bright-eyed and laughing."

Ithaka is an island. "The hills are made of pale cream and purple-gray stone. A woman is carrying a bronze-and-gold bowl with lead loom-weights in it. A woman is carrying a basket full of unwashed wool, greasy and full of grassy debris. A goat with yellow eyes follows you."

The Open Ocean is a room. "The water is dark, the color of unwatered wine. A raft is floating on the ocean. A bird is sitting on the raft. Seaweed is drying on the raft. The wind is coming from the north and smells of snow."

The Underworld is a room. "The air smells of lilies and blood. It is always just past sunset. There is a three-headed dog, spotted like a hyena, lying nearby. A iron double axe-head with dried blood on its blade is sticking out of the rocky ground."

Chapter 2 - Layout

Troy is above the Underworld. Troy is west of Djerba. Troy is northeast of Aeaea. Troy is southwest of Thrinacia. Troy is north of Ogygia. Troy is south of Scheria. Troy is east of Ithaka. Scheria is west of Thrinacia. Scheria is northeast of Ithaka. Scheria is northwest of Djerba. Thrinacia is north of Djerba. Djerba is northeast of Ogygia. Ogygia is east of Aeaea. Ogygia is southeast of Ithaka. Aeaea is west of Ogygia. Aeaea is south of Ithaka.

An island can be free-floating or stabilized. An island is usually free-floating.

After printing the locale description of the Underworld:
	say "You can escape upward.".

After printing the locale description of a free-floating island:
	unless the location is Ithaka, say confusing directions.

After printing the locale description of a stabilized island, say understandable directions.

Instead of going from a free-floating island, sail boldly into the unknown.
Instead of going nowhere from a free-floating island, sail boldly into the unknown.
Instead of going to a free-floating island, sail boldly into the unknown.
Instead of going nowhere from the Open Ocean, sail boldly into the unknown.
Instead of going nowhere from a stabilized island, move the player to the Open Ocean.

To sail boldly into the unknown:
	let Elsewhere be the list of free-floating islands;
	if the location is an island and the location is free-floating:
		remove the location from Elsewhere;
	if the location is not Ithaka:
		remove Ithaka from Elsewhere;
	if the location is not the Underworld:
		add the Underworld to Elsewhere;
	add the Open Ocean to Elsewhere;
	add Elsewhere to Elsewhere;
	add Elsewhere to Elsewhere;
	add Elsewhere to Elsewhere;
	[you become more and more likely to find ithaka as you solve puzzles]
	repeat with unused running through the list of stabilized islands:
		add Ithaka to Elsewhere;
	sort Elsewhere in random order;
	let next be entry 1 of Elsewhere;
	move the player to next.

To stabilize (i - an island):
	now i is stabilized;
	say "The ground rumbles and the breeze stills.[paragraph break][understandable directions]".
	
To say confusing directions:
	say "You think Ithaka might be [one of]north[or]northeast[or]northwest[or]south[or]southwest[or]southeast[or]west[or]east[purely at random] of here, but you're not sure. The stars are all wrong."

To say understandable directions:
	let L be the list of stabilized islands;
	remove location from L;
	if L is empty:
		say "The stars overhead are familiar once again, but you don't know where anything else is.";
	otherwise:
		let D be a list of text;
		repeat with i running through L:
			add "[i] is to [the best route from location to i]" to D;
		say "You're sure that [D]. You could also try sailing in a new direction.".

Book 2 - the Witches

A witch is a kind of woman. A witch has a table name called the dialogue table. A witch has some text called the solution text.

Annoyance is a kind of value. The annoyances are calm, annoyed, exasperated, vexed, irked, irate, and furious.

Needing relates one witch to one thing. The verb to need means the needing relation.
Living on relates one witch to one island. The verb to live on means the living on relation.
Knowing relates one witch to various topics. The verb to know means the knowing relation.

The waterskin is a thing. The waterskin is in Hammerspace. The waterskin can be full or empty. The waterskin is full. Sourcing relates a thing to one room. The verb to be brought from means the sourcing relation. The waterskin is brought from Troy. The printed name of the waterskin is "waterskin ([if full]full of water from [the island to which the waterskin relates by the sourcing relation][otherwise]empty[end if])".

A branch is a kind of thing. A branch can be lit or unlit. A branch is usually unlit.

Chapter 1 - Persephone

Persephone is a witch. Persephone has an annoyance. Persephone is calm.

Table of Persephone's lines
annoyance	line
calm	"'How lovely to see you.' Persephone announces, as if to her court. You feel welcomed, perhaps excessively."
annoyed	"'You again?' Persephone says. She rolls her eyes. You feel embarrassed."
exasperated	"'Don\'t you have anywhere else to be?' Persephone asks. She holds up a hand before you can answer. You close your mouth abruptly."
vexed	"'Please start being interesting.' Persphone says. It is not a suggestion. You look at the floor."
irked	"'What do you think would be a fitting punishment for an uninvited guest,'' Persephone asks. You swallow hard."
irate	"Persephone leans back in her throne, carved from wood as dark as sleep and bone as pale as milk. 'You are no longer welcome here,' she says. The three-headed dog lying next to her raises its head."
furious	"Persephone shrieks, wordless and inhuman, like a fox crying. You curl into a ball around your knees."

Chapter 2 - Andromache

[Problem: Andromache must burn Astyanax's body.
Solution: Bring Andromache a rosemary branch from Aeaea.]

Andromache is a witch. Andromache needs a rosemary branch. Andromache has the solution text "Andromache presses her fist to her lips and holds the rosemary branch to one of the fires smouldering nearby. The branch catches fire. Smoke begins to curl into the air. She takes a deep breath and throws the burning rosemary on a pile of wood nearby.There is a scent of roasting meat and Andromache begins to cry. It is time for you to be on your way, and leave Andromache to her grief, and her captivity. She was a princess of Troy, and beloved, and powerful, and now she is a slave.". Andromache lives on Troy. The dialogue table of Andromache is the Table of Andromache's responses.

Table of Andromache's responses
topic	reply
"herself"	"She nearly spits. 'You've got nerve. You might as well ask after my son, or how I felt as I watched my husband fall in battle and be dishonored by dust.'"
"son/Astynax"	"'He lies unhallowed, and will remain so until I can burn him with proper ceremony.'"
"battle/Troy"	"Arrows shrieking overhead, the weapon of cowards who will only fight from a distance. There is no manhood in that, and yet what can I care for manhood now that my husband is gone and my son as bloody as the day I bore him. Siege is an ugly warfare."
"Hector/Hektor/husband"	"'Keep his name out of your mouth. He was braver than any Achaean, and I will go to my grave knowing it.'"

Test Andromache with "gonear andromache / gonear kirke / take rosemary branch / gonear andromache / give andromache rosemary branch".

Chapter 3 - Adah

[Problem: Djerba's fire has gone out.
Solution: Bring fire from Troy.]

Adah is a witch. Adah needs a piece of driftwood. Adah has the solution text "Adah smiles dreamily and touches your cheek. 'Thank you, and may you reach your destination, O wanderer.' You back away, unnerved by these languid, indifferent folk.". Adah lives on Djerba. The dialogue table of Adah is the Table of Adah's responses.

Table of Adah's responses
topic	reply
"lotos/lotus"	"Adah smiles dreamily."
"name"	"'Adah,' she murmurs."
"fire/brazier"	"Adah gestures to the unlit brazier. [description of the brazier]"

Test Adah with "gonear Adah / gonear andromache / take driftwood / gonear Adah / give Adah driftwood / light driftwood / gonear andromache / light driftwood / gonear Adah / give Adah driftwood".

Chapter 4 - Kirke

[Problem: Kirke's moly is malfunctioning and she can't choose what animal she turns visitors into.
Solution: Bring Kirke fresh water from Scheria.]

Kirke is a witch. Kirke needs a full waterskin brought from Scheria. Kirke has the solution text "She pours the water into the simmering pot suspended over the hearth. The smoke changes color from yellow to white, like new-born lambs when they open their eyes and discover the brightness of the world. She laughs.". Kirke lives on Aeaea. The dialogue table of Kirke is the Table of Kirke's responses.

Table of Kirke's responses
topic	reply
"moly/drugs/potion/mistakes"	"Kirke sniffs. 'My art is as it ever was. My spring has gone sour.'[paragraph break][remember waterskin]"
"deer"	"'Too tall,' Kirke says. 'Spindly-legged. I worry that they'll trip over logs and die immobile in the forest. If only...' She shakes her head. 'No, it was a fine potion.'"
"pigs"	"Kirke raises an eyebrow. 'What pigs, wanderer? Or are you offering an opinion?'[paragraph break]You back down hurriedly from her threat."
"bears"	"'Gluttons. Always stealing my sister-witches' honey. I could tame that. Moly should be enough.'"
"bees"	"'Some of my sister-witches are careless of theirs. I am not.'"
"lions"	"'Stringy-maned.' Kirke then laughs, girlish. 'Still, nothing but big cats in the end. Would you like to pet them?'"
"animals"	"'My menangerie is nothing to be proud of any more, I'm afraid.'"
"wolves"	"'Idiot puppies, really.'"

Test Kirke with "gonear kirke / ask kirke about moly / give kirke waterskin / gonear nausikaa / fill waterskin / ask nausikaa about water / fill waterskin / gonear kirke / give kirke waterskin"

To say remember waterskin:
	If the waterskin is in Hammerspace:
		say "You ask one of your men to fetch a waterskin from the boat. He sprints there and back.";
		move the waterskin to the player.

To allow Kirke to be picky:
	if the waterskin is empty:
		say "She needs water, not an empty waterskin.";
		stop the action;
	if the waterskin is not brought from Scheria:
		let s be the the island to which the waterskin relates by the sourcing relation;
		choose the row with source of s from the Table of Kirke refusing water;
		say "Kirke frowns. '[refusal entry]'[paragraph break]";
		stop the action;
	continue the action.

Table of Kirke refusing water
source	refusal
Troy	"Dead water full of dead things."
Djerba	"Lotos-juice."
Aeaea	"My own water does not cure me."
Thrinacia	"The sun burns, it does not sweeten."
Ogygia	"My sister-witch would have helped already if she had the power."
Ithaka	"Blood and mockery."
Open Ocean	"Salt poison."
Underworld	"The queen of the dead does not heal."

Chapter 5 - Lampetia

Lampetia is a witch. The dialogue table of Lampetia is the Table of Lampetia's responses. Lampetia lives on Thrinacia.

Table of Lampetia's responses
topic	reply
"cows"	"My father's, but do not harm them. The last man who spilled their blood still flees the Kindly Ones."
"bees"	"[one of]Few too many of them if you ask me.[or]My father doesn't care one whit for them, and neither do I.[at random]"
"night and day"	"Lampetia nods.[paragraph break][stabilize Thrinacia]"

To say Lampetia's riddle:
	If Thrinacia is free-floating:
		say "There are two sisters: one gives birth to the other who then gives birth to the first. Who are they?".

Report asking Lampetia about something:
	say Lampetia's riddle;
	stop the action.

Report telling Lampetia about something:
	say Lampetia's riddle;
	stop the action.

Report answering Lampetia that something:
	say Lampetia's riddle;
	stop the action.

To say stabilize Thrinacia:
	stabilize Thrinacia;
	stop the action.

Test Lampetia with "gonear Lampetia / ask Lampetia about cows / ask lampetia about bees / answer lampetia night and day".

Chapter 6 - Calypso

[Problem: Calypso's wine is sour.
Solution: Bring Calypso bees from Thrinacia.]

Calypso is a witch. Calypso needs the beehive. Calypso has a solution text "Calypso claps her hands together. The bees begin to hum more loudly, and she laughs, before tying her feather-hair back with a scrap of linen. 'You are welcome, cousins,' she says, and the bees begin to swarm around her head.". Calypso lives on Ogygia. The dialogue table of Calypso is the table of Calypso's responses.

Table of Calypso's responses
topic	reply
"wine"	"Calypso grimaces. 'I'd offer you some, but it's gone thin and sour.'"
"thin/sour"	"'My bees all died, you see. And my cousin is jealous of her animals.'"
"feathers/birds"	"Calypso laughs. 'What of them?'"
"apple"	"'Beautiful, aren't they,' Calypso replies. 'And ordinarily they make a lovely wine.'"
"cypress/war/dusk/blood"	"'Oh, don't talk to me of mourning, or dusk, or any of that nonsense!' Calypso says. 'You men. You think nothing is serious until blood's involved.'"

Test Calypso with "gonear calypso / gonear lampetia / follow bees / take beehive / gonear calypso / give calypso beehive".

Chapter 7 - Nausikaa

Nausikaa is a witch. The dialogue table of Nausikaa is the Table of Nausikaa's responses. Nausikaa can be trusting or skeptical. Nausikaa is skeptical. The solution text of Nausikaa is "The wariness fades from Nausikaa's expression. ". Nausikaa lives on Scheria.

Before speech in the presence of Nausikaa:
	Unless Nausikaa is trusting or the topic understood includes "dogs":
		say "Nausikaa frowns at you skeptically." instead.

Table of Nausikaa's responses
topic	reply
"dogs"	"Nausikaa can't hide her smile. 'My father breeds the best hounds. This is Eurymedusa, I named her when I was little, after my nurse from Aperaea, and this is Charis, my brother's favorite, she can track a bird through the air, and just see how pretty she is, all dappled like shadows.'"
"fabric/cloth/laundry/water"	"'This is where I go swimming with my sisters, and where we wash our best clothes. It's a good place for ball games while the laundry dries.'[learn about water]"
"name/self/introduction/banquet"	"'I don't see why I should talk to you.' Nausikaa says."
"beer/hospitality/drink"	"Nausikaa looks uncomfortable, torn between the demands of hospitality and a fear of... something. Gossip?"

To say learn about water:
	now Scheria is watery;

Test Nausikaa with "gonear nausikaa / ask nausikaa about water / ask nausikaa about dogs / pet dogs / ask nausikaa about water".

Chapter 8 - Penelope

Penelope is a witch. The dialogue table of Penelope is the Table of Penelope's responses.

Table of Penelope's responses
topic	reply
"silence/nothing"	"[win game]"

Before speech in the presence of Penelope for the first time:
	say "You greet your wife. She turns her head away. 'Do not speak and you say my name. If you need to speak, you will say my name.'";
	stop the action.	

To say Penelope's riddle:
	say "[one of]Penelope interrupts you. [or]Penelope cuts you off with a sharp shake of her head. [at random]'Do not speak and you say my name. If you need to speak, you will say my name.'".

Report answering Penelope that something:
	say Penelope's riddle;
	stop the action.

To say win game:
	end the story saying "Penelope has recognized you. Your journey home has ended. The suitors must still be dealt with, and your father mourns your in the orchard you planted with him long ago, but for now, you can rest; tomorrow is time enough to seek glory. This island knows you now, and you are home." instead.

Test Penelope with "gonear penelope / answer penelope that lies / answer penelope silence".
 
Book 3 - the Islands, and other Lands

A room can be watery or arid. A room is usually watery.

Chapter 1 - the Underworld

Persephone is in the Underworld. "[if the Underworld is unvisited]Persephone's biding her time here until spring. This is her country, and you know to tread carefully. Even heroes have no rights here.[otherwise]Persephone is still here. All men must come to the realm of the dead one day, but you had hoped to find your way home first, even perhaps to know your name would live glorious beyond you.[end if] [entrance line]"

Instead of speech in the presence of Persephone, say entrance line. Instead of examining Persephone, say entrance line.

To say entrance line:
	let annoyance level be the annoyance of Persephone;
	let new line be the line corresponding to an annoyance of annoyance level in the Table of Persephone's lines;
	say new line.
	
Before going to the Underworld:
	if the Underworld is visited, increase Persephone's annoyance.

To increase Persephone's annoyance:
	if the annoyance of Persephone is less than furious, now the annoyance of Persephone is the annoyance after the annoyance of Persephone.
			
Chapter 2 - Troy

Andromache is in Troy. "Andromache stands unbowed before you." Instead of examining Andromache, try asking Andromache about "herself".

The city is scenery in Troy. The hill is scenery in Troy. The walls are scenery in Troy. Scorch marks are scenery in Troy. Bodies are scenery in Troy. Arrowheads are scenery in Troy. Spear-shafts are scenery in Troy. Fire is scenery in Troy. Understand "fires" as fire.

A piece of driftwood is a branch. A piece of driftwood is in Troy. "Driftwood lies scattered about the dead beach."

Instead of burning the piece of driftwood:
	if the location is Troy:
		now the driftwood is lit;
		say "You turn the driftwood into a makeshift torch.";
	otherwise:
		say "You can't see any fire to light that from.".

Chapter 3 - Djerba

Adah is in Djerba. "A woman is wearing a white khiton. She is wearing gold and bronze pins in her hair." Understand "woman" as Adah. Instead of examining Adah, try asking Adah about "name".

The brazier is a thing that is fixed in place. The brazier is in Djerba. "An unlit brazier." The description of the brazier is "It is three-legged, made out of bronze with etched and painted geometric patterns on the outside. You can see the figure of a horse emerging from the shapes, its proud head lifted as it stretches itself to run."

Chapter 4 - Aeaea

Kirke is in Aeaea. "You see Kirke amongst her animals, idly scratching a wolf's head." Instead of examining Kirke, say "Kirke looks at you sharply. 'Do you think of me as meat for market?'".

The rosemary branch is a thing. The rosemary branch is in Aeaea.

Instead of taking the swarm in the presence of Kirke, try asking Kirke about "bees". Instead of examining the swarm in the presence of Kirke, try asking Kirke about "bees".

The deer is scenery in Aeaea. Instead of examining the deer, try asking Kirke about "deer". Instead of petting the deer, say "They shy away from you.".

The pig is scenery in Aeaea. Understand "pigs" as pig. Instead of examining the pig, try asking Kirke about "pigs". Instead of petting the pig, say "What pig?".

The bear is scenery in Aeaea. Understand "bears" as bear. Instead of examining the bear, try asking Kirke about "bears". Instead of petting the bear, say "You do not trust Kirke enough.".

The lion is scenery in Aeaea. Understand "lions" as lion. Instead of examining the lion, try asking Kirke about "lion". Instead of petting the lion, say "You do not trust Kirke enough."

The wolf is scenery in Aeaea. Understand "wolves" as wolf. Instead of examining the lion, try asking Kirke about "wolves". Instead of petting the wolf, say "You do not trust Kirke enough."


Chapter 5 - Thrinacia

Lampetia is in Thrinacia. "Lampetia's here. Her tanned skin seems to glow." Instead of examining Lampetia, say "Her expression is placid, but her stance is wary."

The swarm is a thing. Understand "bees" as the swarm. Understand "bee" as the swarm. The swarm is in Thrinacia. "Bees fill the air and stream toward a gap in the trees." Instead of taking the swarm, say "A bee stings your leather glove and dies." Instead of examining the swarm, try following the swarm. Instead of petting the swarm, try taking the swarm.

The beehive is a thing. The beehive is in Hammerspace.

Instead of following the swarm:
	if the beehive is in Hammerspace or the beehive is in Thrinacia:
		say "You follow the bees over the island, gingerly sidestepping cows and their dung. You find a beehive, high in a tree.";
		move the beehive to Thrinacia;
	otherwise:
		say "The bees mill about aimlessly."

Chapter 6 - Ogygia

Calypso is in Ogygia. "Calypso’s hair is made of feathers." Instead of examining Calypso, say "Calypso returns your gaze, scanning you up and down long enough for it to be uncanny. 'Ah, but it's rude to stare so,' she interrupts herself."

Apple trees are scenery in Ogygia. Understand "apple" as apple trees. Understand "apples" as apple trees. Instead of examining the apple trees, try asking Calypso about "apple". Understand "fruit" as apple trees.

Cypress trees are scenery in Ogygia. Understand "cypress" as cypress trees. The description of the cypress trees is "Stark, and too tall. They carry dusk with them."

Chapter 7 - Scheria

Nausikaa is in Scheria. "[if Nausikaa is skeptical]Nausikaa is here. You met her once, even younger than now, at a banquet. Perhaps she doesn't recognize you. Perhaps you made a bad impression, then.[otherwise]Nausikaa is sitting, cross-legged, braiding her hair.[end if]". Instead of examining Nausikaa, try asking Nausikaa about "name".

The dogs are a thing. The dogs are in Scheria. "Fawn-colored sighthounds lie in the shade." Understand "dog" as dogs. Understand "sighthounds" as dogs. Understand "sighthound" as dogs. Understand "Eurymedusa" as dogs. Understand "Charis" as dogs. Instead of examining the dogs, try asking Nausikaa about "dogs".

Scheria is arid.

The rhyton is scenery in Scheria. The description of the rhyton is "Horn, barely yellowed, barely translucent. The dog's eyes are dark-colored from the beer behind them."

Beer is scenery in Scheria. The description of beer is "Fresh, from the smell of it." Instead of drinking the beer, say "You'd rather not presume on Nausikaa's hospitality. Her parents would know." Instead of taking the beer, try drinking the beer.

The fabric is scenery in Scheria. The description of fabric is "Newly laundered and half-dry."

After examining the fabric:
	say "Nausikaa notices you thumbing the fabric, and raises a hand to stop you.";
	try asking Nausikaa about "fabric".	

The boys are scenery in Scheria. The description of boys is "Not old enough for war, yet, and therefore still lucky." 

Chapter 8 - Ithaka

Penelope is in Ithaka. "Penelope is among her women, her veil drawn across her face. Her maids are chattering as they wind wool."

Book 4 - Interactions

Chapter 1 - Speech

Asking someone about something is speech. Telling someone about something is speech. Answering someone that something is speech. Asking someone for something is speech.

Carry out telling a witch about something:
	repeat through the dialogue table of the noun:
		if the topic understood includes topic entry:
			say "[reply entry][paragraph break]";
			rule succeeds;
	if the noun is not Penelope, say "[the noun] looks at you blankly.";
	stop the action.

Report telling a witch about something:
	stop the action instead.

Carry out asking a witch about something:
	repeat through the dialogue table of the noun:
		if the topic understood includes topic entry:
			say "[reply entry][paragraph break]";
			rule succeeds;
	if the noun is not Penelope, say "[the noun] looks at you blankly.";
	stop the action.

Report asking a witch about something:
	stop the action instead.

Carry out answering a witch that something:
	repeat through the dialogue table of the noun:
		if the topic understood includes topic entry:
			say "[reply entry][paragraph break]";
			rule succeeds;
	if the noun is not Penelope, say "[the noun] looks at you blankly.";
	stop the action.

Report answering a witch that something:
	stop the action instead.

Chapter 2 - Barter Puzzles

The block giving rule is not listed in the check giving it to rules.

Check giving (this is the polite refusal of unwanted objects rule):
	unless the noun interests the second noun: 
		say "[The second noun] refuses [the noun]." instead.

Report giving:
	say "You give [the second noun] [the noun]. She smiles."

After giving:
	solve the problem of the second noun.

To decide whether (item - a thing) interests (character - a person):
	if the character is Andromache and the item is the piece of driftwood:
		say "Andromache's nostrils flare. 'I can collect that myself. Not good enough.'";
		no;
	if the character is Kirke and the item is the waterskin and the waterskin is full and the waterskin is brought from Scheria, yes;
	if the character is Kirke and the item is the waterskin:
		allow Kirke to be picky;
		no;
	if the character is Adah and the item is the piece of driftwood and the item is lit, yes;
	if the character is Adah and the item is the piece of driftwood and the item is unlit, no;
	if the character needs the item, yes;
	no.

To solve the problem of (witch - a witch):
	Say the solution text of the witch;
	Stabilize the island to which the witch relates by the living on relation.
	
Chapter 3 - Actions

Emptying is an action applying to one carried thing. Understand "empty [something preferably held]" as emptying.

Check emptying:
	unless the noun is the waterskin, say "You cannot empty [the noun]" instead;
	unless the noun is full, say "It's already empty" instead.

Carry out emptying: now the noun is empty. Report emptying: say "You dump water onto the sand.".

Filling is an action applying to one carried thing. Understand "fill [something preferably held]" as filling.

Before filling: if the noun is the waterskin and the noun is full, try emptying the waterskin.

Check filling:	
	unless the noun is the waterskin, say "You cannot fill [the noun]" instead;
	unless the noun is empty, say "It's already full" instead;
	unless the location is watery, say "You can't see any water from which to fill the waterskin." instead.

Carry out filling:
	now the noun is full;
	now the noun is brought from location.

Report filling:
	if location is the Open Ocean:
		say "You fill the waterskin with brine.";
	otherwise:
		say "You fill the waterskin from the springs of [location].".

Following is an action applying to one visible thing. Understand "follow [something]" as following.

Understand "scrounge for [something]" as taking. Understand "scrounge [something]" as taking.

Petting is an action applying to one thing. Understand "pet [something]" as petting.

Check petting:
	unless the noun is dogs, say "That doesn't seem like a good idea." instead.

Carry out petting:
	say "You gently scratch behind their ears. The dogs yelp happily and press closer, nosing at you.[paragraph break]";
	if Nausikaa is skeptical:
		now Nausikaa is trusting;
		solve the problem of Nausikaa.
